OnTranslate
{
	_talk = reference0
	
	//This is what makes %(embedded_elements) work in script input
	if reference1 == "" && reference2 == "" //If this is from the input box
	{ //send input box : no event (ref2) , no special flag (ref1)
		_talk = EVAL('"' + REPLACE(_talk,'"','""') + '"')
	}
	
	//If you want to add anything to OnTranslate, like text replacement, I would put it after this point, generally. Just make sure it's before the TOSTR(_talk)
	
	if gamestate != 1
	{
		if RE_SEARCH(_talk,"^\\1"); _talk = RE_REPLACE(_talk,"^\\1","\1\![set,balloonwait,105%]")
		elseif RE_SEARCH(_talk,"^\0"); _talk = RE_REPLACE(_talk,"^\0","\0\![set,balloonwait,105%]")
	}
	if gamestate == 4 && reference3[0,C_BYTE1] == "restore"
	{
		_talk = RE_REPLACE(_talk,"\\w\d","")
		_talk = RE_REPLACE(_talk,"\\s\[\d+\]","")
		_talk = RE_REPLACE(_talk,"\\i\[\d+\]","")
	}
	
	TOSTR(_talk)
}

//This is just here so you can use anchors to link to websites if you want. You can link to websites with normal menu choice tags! If you do, a handy URL preview will show up in the balloon!
OnAnchorSelect
{
	if "http://" _in_ reference0 || "https://" _in_ reference0; "\j[""%(reference0)""]"
}

//Hotkeys
OnKeyPress
{
	if reference0 == "f1"; "\![open,readme]"
	if gamestate == 0
	{	
		if reference0 == "up" || reference0 == "left" || reference0 == "right" || reference0 == "down"
		{"\![raise,OnMove,%(reference0)]"}
	}
}

//Happens about 30 seconds after a script ends, by default, to bring them back to normal poses
OnSurfaceRestore
{
	"%(s.init)"
}

//Happens when the ghost is unminimized. You *can* include dialogue, but you don't *have* to. I have included this event because if you don't, it defaults to \0\s[0]\1\s[10], which you don't want in some cases, such as if your ghost uses different surfaces or has a different number of characters.
OnWindowStateRestore
{
	OnSurfaceRestore
}

s.init : all
{
	_fade = 0
	if SHIORI3FW.Eventid == "OnBoot"; _fade = 1

	"\![reset,sticky-window]\![set,sticky-window,0,1]" 
	"\0\s[10]\![move,--X=0,--Y=0,--base=0,--option=ignore-sticky-window]"
	if gamestate == 1; "\1\s[2000]\![move,--X=0,--Y=0,--base=0,--option=ignore-sticky-window]"
	else; "\1\s[1000]\![move,--X=0,--Y=0,--base=0,--option=ignore-sticky-window]"
	
	if _fade; "%(fadein)"
	else; "\![set,alpha,100]"
	
	case gamestate
	{
		when 1; "\0\s[40]" //dream archive
		when 2 { 			//nightmare study
			if !IsAkari; 	"\0\s[20]"
			else;			"\0\s[22]\i[51]" 
			//dear god. please don't look at this indenting its so ugly
		}
		when 3; "\0\s[42]" //journal 
							//TODO: i'd love to make a special lectern for this instead of reusing my freeshell LOL
		when 4; "\0\s[0]" //puzzle
		others; "\0\s[40]" //normal gameplay
	}
	
	if _fade; "%(fadein)"
	else; "\![set,alpha,100]"
}

fadein : all
{
	for _i = 0; _i < 100; _i++
	{
		"\![set,alpha,%(_i)]\w[10]"
	}
	"\![set,alpha,100]"
}
fadeout : all
{
	for _i = 100; _i > 0; _i--
	{
		"\![set,alpha,%(_i)]\w[10]"
	}
	"\![set,alpha,0]"
}


//Network update url. Make sure you have a / on the end of the url or it will give a warning every time users try to update! If you don't plan to use network updates you can remove this
//IF YOU'RE USING GITHUB FOR NETWORK UPDATES: DO NOT USE github.io URLS! It will cause you pain and suffering. USE raw.githubusercontent!!!
//To get the raw.githubusercontent link super easy:
//• Open your ghost's repo online
//• navigate to the ghost's folder (the top level one, with install.txt, readme.txt, etc)
//• Click on install.txt, or another text file on that level
//• Once on that page, click the "raw" button. You should see the url now starts with raw.githubusercontent
//• Copy that url exactly as is, but *leave off the "install.txt" or whatever file name at the end.*
//Tada! That's your url! Make sure you test it! The dev palette and script/network log can help with that
On_homeurl
{
	"https://raw.githubusercontent.com/WhatAPhantasia/Dreamcatchergaka/master/da_whereareyoudreamcatcher/"
}


//—————————————————————————————— Right click menu links ——————————————————————————————

//Takes an array of alternating names and urls, and formats them for use with the right click menu
FormatLinks : all
{
	for _i = 0; _i < ARRAYSIZE(_argv); _i++
	{
		_argv[_i] //Add the link/title
		//Alternate between adding %ASC(1) or %ASC(2)
		if _i % 2 == 1; "%ASC(2)"
		else; "%ASC(1)"
	}
}

On_sakura.recommendsites
{
	FormatLinks(recommendsites_sakura)
}

//These are the websites linked in the top option of the sakura's right click menu. The format is simple; write the label of the link on the left followed by a semicolon, and then on the right, the url to link to. These should be separate strings.
recommendsites_sakura : array
{
	"Nightmare's Toyhou.se Page";	"https://toyhou.se/20808237.nightmare"
	"Dreamcatcher Archives Toyhou.se Folder";	"https://toyhou.se/WhatAPhantasia/characters/folder:3533735"
}

On_sakura.portalsites 
{
	FormatLinks(portalsites_sakura)
}

//These are the websites linked in the second option of the sakura's right click menu. Same as above
portalsites_sakura : array
{
	"Download Page"; "https://whataphantasia.github.io/projects/ukagaka/ghosts/da-where-are-you-dreamcatcher"
	"Changelog (G. Docs)"; "https://docs.google.com/document/d/10yy69A5hkizTPMh1mmRISIDg8M3JMfOdL9abrYmme20/edit?usp=sharing"
	"Phantasia's Website"; "https://whataphantasia.github.io/"
	""; ""
	"Simplicity v1.0.8"; "https://github.com/Zichqec/simplicity_template"
	"Ukagaka Dream Team Wiki"; "https://ukagakadreamteam.com/wiki/"
}

On_kero.recommendsites
{
	FormatLinks(recommendsites_kero)
}

//These are the websites linked in the first option of the kero's right click menu. Same as above
recommendsites_kero : array
{
	"Dreamcatcher Archives Toyhou.se Folder";	"https://toyhou.se/WhatAPhantasia/characters/folder:3533735"
}

//to match up with what user put in ssp prefs.
username
{
	SHIORI3FW.UserName
}

Capitalize
{
	_word = _argv[0] //Sets _word to the word the user sent
	_buffer = SUBSTR(_word,0,1) //Storing the first character in _buffer
	_word = ERASE(_word,0,1) //Erasing the first character from _word
	_word = INSERT(_word,0,TOUPPER(_buffer)) //Making the character in _buffer uppercase and inserting it back into _word
	_word //Returns the capitalized word
}

//-----other functions.
IsAkari
{
	_user = CleanTerm(user)
	if _user == "akari" || _user == "kuromiya" || _user == "ia"; 1
	else; 0
}

seenallstudy
{
	if Flag("Done.Study.Dream") && Flag("Done.Study.Nightmare") && Flag("Done.Study.Asibikaashi") && Flag("Done.Study.Dreamworld") && Flag("Done.Study.Self.1")&& Flag("Done.Study.Self.2"); 1
	else; 0
}

//basically the name cleaning function in OnTitle and OnName. returns the cleaned term!!
CleanTerm
{	
	_term = _argv[0] //putting the raw input into _term to do some tests on it
	_term = TOLOWER(_term) //changes it to lowercase
	_term = REPLACE(_term," ","") //removes spaces
	_term = REPLACE(_term,".","") //removes periods
	_term = REPLACE(_term,"-","") //removes dashes
	_term
}

//thank you zi
BalloonIsOpen
{
	"balloon" _in_ var.req.value[ASEARCH("Status",var.req.key)]
}

//Written by Zichqec https://ukagaka.zichqec.com/

Flag //Checks to see if a flag exists. Returns 1 if yes, returns 0 if no
{
	if ASEARCH(_argv[0],MiscFlags) != -1; 1
	else; 0
}

RemoveFlag //Removes all instances of a flag in the array.
{
	_elements = ASEARCHEX(_argv[0],MiscFlags)
	if ARRAYSIZE(_elements) > 0
	{
		for _i = ARRAYSIZE(_elements) - 1; _i >= 0; _i-- //Goes from the end of the list to the start, so the elements stay in the same positions while we erase
		{
			_current = _elements[_i]
			MiscFlags[_current] = IARRAY
		}
	}
}

MapFlag //Same but for MapFlags.
{
	if ASEARCH(_argv[0],MapFlags) != -1; 1
	else; 0
}

RemoveMapFlag //Removes all instances of a flag in the array.
{
	_elements = ASEARCHEX(_argv[0],MapFlags)
	if ARRAYSIZE(_elements) > 0
	{
		for _i = ARRAYSIZE(_elements) - 1; _i >= 0; _i-- //Goes from the end of the list to the start, so the elements stay in the same positions while we erase
		{
			_current = _elements[_i]
			MapFlags[_current] = IARRAY
		}
	}
}